;****************************************************************************
; GetStrLen
; Gets the length of a string.
;----------------------------------------------------------------------------
; input a6.l .... Pointer to string
;----------------------------------------------------------------------------
; output d7.w ... String length
;****************************************************************************

GetStrLen:
    move.l  a6, -(sp)                   ; Chances are we want it again
    moveq   #0, d7                      ; Reset character count
    
@Loop:                                  ; Keep counting characters until we
    tst.b   (a6)+                         ; hit nul (end of string)
    beq.s   @Done
    addq.w  #1, d7
    bra.s   @Loop
    
@Done:
    move.l  (sp)+, a6                   ; Restore this pointer
    rts                                 ; End of subroutine

;****************************************************************************
; CopyStr
; Copies a string elsewhere.
;----------------------------------------------------------------------------
; input a6.l .... Source pointer
; input a5.l .... Destination pointer
;----------------------------------------------------------------------------
; output a6.l ... End of source
; output a5.l ... End of destination
;****************************************************************************

CopyStr:
@Loop:
    move.b  (a6)+, (a5)+
    bne.s   @Loop
    rts

;****************************************************************************
; CopyStrNoNull
; Copies a string elsewhere without the nul terminator
;----------------------------------------------------------------------------
; input a6.l .... Source pointer
; input a5.l .... Destination pointer
;----------------------------------------------------------------------------
; output a6.l ... End of source (at nul!)
; output a5.l ... End of destination
;****************************************************************************

CopyStrNoNull:
@Loop:
    tst.b   (a6)
    beq.s   @End
    move.b  (a6)+, (a5)+
    bra.s   @Loop
@End:
    rts
