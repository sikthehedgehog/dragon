;****************************************************************************
; InitPotion
; Initializes a potion's collision box (also crosses due to being similar).
;----------------------------------------------------------------------------
; input a6.l ... Pointer to object
;----------------------------------------------------------------------------
; breaks: d5-d7, a4-a6
;****************************************************************************

InitPotion:
    move.w  #-12, OBJ_BOXX1(a6)
    move.w  #-8, OBJ_BOXY1(a6)
    move.w  #11, OBJ_BOXX2(a6)
    move.w  #15, OBJ_BOXY2(a6)
    rts

;****************************************************************************
; RunRedPotion
; Executes the behavior for a red potion.
;----------------------------------------------------------------------------
; input a0.l ... Pointer to object
;----------------------------------------------------------------------------
; breaks: all but a0
;****************************************************************************

RunRedPotion:
    move.l  a0, a6                      ; Taken by the player?
    bsr     (CollidesPlayer)
    tst.w   d7
    bne.s   @Taken
    rts

@Taken:
    moveq   #SFX_HEALTH, d7             ; Make noise
    jsr     (PlaySFX).w
    move.l  #SCORE_ITEM, d7             ; Bonus
    bsr     (AddScore)
    
    move.b  #3, (PlayerHealth)          ; Refill the player's health
    move.w  #$18, (HealthBlink)
    
    bra     DestroySelf                 ; We're gone!

;****************************************************************************
; RunYellowPotion
; Executes the behavior for a yellow potion.
;----------------------------------------------------------------------------
; input a0.l ... Pointer to object
;----------------------------------------------------------------------------
; breaks: all but a0
;****************************************************************************

RunYellowPotion:
    move.l  a0, a6                      ; Taken by the player?
    bsr     (CollidesPlayer)
    tst.w   d7
    bne.s   @Taken
    rts

@Taken:
    moveq   #SFX_SUPERMAGIC, d7         ; Make noise
    jsr     (PlaySFX).w
    move.l  #SCORE_ITEM, d7             ; Bonus
    bsr     (AddScore)
    
    move.w  #20*60, (Supermagic)        ; Supermagiiiiic!
    bra     DestroySelf                 ; We're gone!

;****************************************************************************
; RunCross
; Executes the behavior for a cross.
;----------------------------------------------------------------------------
; input a0.l ... Pointer to object
;----------------------------------------------------------------------------
; breaks: all but a0
;****************************************************************************

RunCross:
    move.l  a0, a6                      ; Taken by the player?
    bsr     (CollidesPlayer)
    tst.w   d7
    bne.s   @Taken
    rts

@Taken:
    move.l  #SCORE_ITEM, d7             ; Bonus
    bsr     (AddScore)
    
    bsr     AddLife                     ; Extra life! :D
    bra     DestroySelf                 ; We're gone!

;****************************************************************************
; DrawPotion
; Draws a potion item (also crosses due to being similar).
;----------------------------------------------------------------------------
; input a0.l ... Pointer to object
;----------------------------------------------------------------------------
; breaks: all but a0
;****************************************************************************

DrawPotion:
    move.w  OBJ_X(a0), d0               ; Determine sprite coordinate
    move.w  OBJ_Y(a0), d1
    sub.w   (CameraX), d0
    sub.w   (CameraY), d1
    sub.w   #12, d0
    sub.w   #8, d1
    
    move.w  #VramRedPotion, d2          ; What color is it?
    moveq   #0, d7
    move.b  OBJ_TYPE(a0), d7
    sub.b   #OBJTYPE_RPOTION, d7
    add.w   d7, d2
    lsl.w   #3, d7
    add.w   d7, d2
    
    moveq   #%1010, d3                  ; Draw the sprite
    jsr     (AddSprite).w
    
    rts                                 ; End of subroutine

;****************************************************************************
; InitKey
; Initializes a key's collision box
;----------------------------------------------------------------------------
; input a6.l ... Pointer to object
;----------------------------------------------------------------------------
; breaks: d5-d7, a4-a6
;****************************************************************************

InitKey:
    tst.b   (HasKey)                    ; Don't spawn if we already have
    beq.s   @NoKeyYet                     ; the key...
    clr.b   OBJ_TYPE(a6)
@NoKeyYet:
    
    move.w  #-12, OBJ_BOXX1(a6)         ; Collision box
    move.w  #-8, OBJ_BOXY1(a6)
    move.w  #11, OBJ_BOXX2(a6)
    move.w  #15, OBJ_BOXY2(a6)
    
    rts                                 ; End of subroutine

;****************************************************************************
; RunKey
; Executes the behavior for a key.
;----------------------------------------------------------------------------
; input a0.l ... Pointer to object
;----------------------------------------------------------------------------
; breaks: all but a0
;****************************************************************************

RunKey:
    move.l  a0, a6                      ; Taken by the player?
    bsr     (CollidesPlayer)
    tst.w   d7
    bne.s   @Taken
    rts

@Taken:
    move.l  #SCORE_ITEM, d7             ; Bonus
    bsr     (AddScore)
    
    st.b    (HasKey)                    ; Got the key!
    moveq   #SFX_KEY, d7
    jsr     (PlaySFX).w
    
    bra     DestroySelf                 ; We're gone!

;****************************************************************************
; DrawKey
; Draws a key.
;----------------------------------------------------------------------------
; input a0.l ... Pointer to object
;----------------------------------------------------------------------------
; breaks: all but a0
;****************************************************************************

DrawKey:
    move.w  OBJ_X(a0), d0               ; Determine sprite coordinate
    move.w  OBJ_Y(a0), d1
    sub.w   (CameraX), d0
    sub.w   (CameraY), d1
    subq.w  #8, d0
    subq.w  #8, d1
    
    move.w  (Anim), d7                  ; Make key float
    lsr.w   #2, d7
    and.w   #$03<<1, d7
    lea     @Wobble(pc), a6
    add.w   (a6,d7.w), d1
    
    move.w  #VramKey, d2                ; Draw the sprite
    moveq   #%0110, d3
    jsr     (AddSprite).w
    
    rts                                 ; End of subroutine

;----------------------------------------------------------------------------

@Wobble:
    dc.w    0, -1, 0, 1
