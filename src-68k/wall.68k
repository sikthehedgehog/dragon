;****************************************************************************
; WallSpeed
; Speed at which the moving wall advances for each difficulty
;****************************************************************************

WallSpeed:
    dc.w    $100, $140, $180

;****************************************************************************
; RunWall
; Executes the behavior of the moving wall.
;----------------------------------------------------------------------------
; input a0.l ... Pointer to object
;----------------------------------------------------------------------------
; breaks: all but a0
;****************************************************************************

RunWall:
    moveq   #0, d7                      ; Determine how fast we move
    move.b  (Difficulty), d7
    add.w   d7, d7
    lea     WallSpeed(pc), a6
    move.w  (a6,d7.w), d7
    move.w  d7, d1
    add.w   (Subpixel), d7
    asr.w   #8, d7
    
    move.w  OBJ_X(a0), d0               ; Move wall!
    add.w   d7, d0
    move.w  d0, OBJ_X(a0)
    move.w  d0, (WallPos)
    
    add.w   #-PLAYER_X1, d0             ; Push the player forwards?
    cmp.w   (PlayerX), d0
    ble.s   @NoPush
    move.w  d0, (PlayerX)
    move.w  d1, (PlayerXSpeed)
    
    move.w  (PlayerX), d7               ; Crushed the player?
    move.w  (PlayerY), d6
    bsr     TileAt
    cmp.b   #COLL_SOLID, d6
    bne.s   @NoPush
    bsr     KillPlayer
    
@NoPush:
    move.w  (QuakeTimer), d7            ; The room is shaking!!
    and.w   #$03, d7
    or.w    #$04, d7
    move.w  d7, (QuakeTimer)
    
    move.w  OBJ_TIMER(a0), d7           ; Spawn derbis every so often
    addq.w  #1, d7
    and.w   #$03, d7
    move.w  d7, OBJ_TIMER(a0)
    bne.s   @NoDerbis
    
    jsr     (Rand).w
    move.w  d7, d6
    jsr     (Rand).w
    and.w   #$7F, d6
    and.w   #$FF, d7
    add.w   d6, d7
    add.w   (CameraX), d7
    move.w  (CameraY), d6
    moveq   #OBJTYPE_DERBIS, d5
    bsr     AddObject
    jsr     (Rand).w
    and.w   #$FF, d7
    add.w   #$100, d7
    move.w  d7, OBJ_YSPEED(a6)
    
@NoDerbis:
    move.w  OBJ_TIMER(a0), d7           ; Make noise
    and.w   #$07, d7
    bne.s   @NoNoise
    moveq   #SFX_QUAKE, d7
    jsr     (PlaySFX).w
    
@NoNoise:
    rts                                 ; End of subroutine
